plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "ru.maxim.barybians"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "@string/app_name_debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "@string/app_name_release"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures{
        dataBinding true
        viewBinding true
    }
}

dependencies {
    def nav_version = "2.4.0"
    def room_version = "2.4.1"
    def livedata_version = "2.4.0"
    def coroutines_version = '1.6.0-native-mt'
    def dagger_version = '2.40.5'
    def moxy_version = "2.2.2"

    // KTX
    implementation 'androidx.core:core-ktx:1.7.0'

    // DI
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // AndroidX
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.android.support:multidex:1.0.3"
    implementation "com.google.android.material:material:1.5.0"

    // MVP
    kapt "com.github.moxy-community:moxy-compiler:$moxy_version"
    implementation "com.github.moxy-community:moxy:$moxy_version"
    implementation "com.github.moxy-community:moxy-androidx:$moxy_version"
    implementation "com.github.moxy-community:moxy-ktx:$moxy_version"

    // Adapter delegates
    implementation 'com.hannesdorfmann:adapterdelegates4:4.3.1'

    // Images
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    // Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Livecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$livedata_version"

    // View binding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0-native-mt"

    // Network
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3'

    // Permissions
    implementation 'com.karumi:dexter:6.2.3'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Tests
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}
