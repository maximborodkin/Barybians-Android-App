apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "ru.maxim.barybians"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "@string/app_name_debug"
        }
        release {
            minifyEnabled false
            shrinkResources false
            resValue "string", "app_name", "@string/app_name_release"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    buildFeatures{
        dataBinding = true
    }
}

dependencies {
    def room_version = "2.2.5"
    def okhttpVersion = "3.12.12"
    // General
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.4"

    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:1.3.2"

    // Support
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.2"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.android.support:multidex:1.0.3"

    // MVP
    kapt "tech.schoolhelper:moxy-x-compiler:1.7.0"
    implementation "tech.schoolhelper:moxy-x-material:1.7.0"
    implementation "tech.schoolhelper:moxy-x:1.7.0"
    implementation "tech.schoolhelper:moxy-x-androidx:1.7.0"

    // Images
    kapt "com.github.bumptech.glide:compiler:4.11.0"
    implementation "com.github.bumptech.glide:glide:4.11.0"

    // Design
    implementation "com.google.android.material:material:1.3.0-alpha03"

    // Database
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"

    // Network
    implementation ("com.squareup.okhttp3:okhttp") {
        version { strictly (okhttpVersion) }
    }
    implementation ("com.squareup.okhttp3:logging-interceptor") {
        version { strictly (okhttpVersion) }
    }
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"

    // Permissions
    implementation "com.karumi:dexter:6.2.1"
}
